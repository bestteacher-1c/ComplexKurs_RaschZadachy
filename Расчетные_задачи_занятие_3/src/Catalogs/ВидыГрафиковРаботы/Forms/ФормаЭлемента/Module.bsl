&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗаполнитьСписокДнейНедели();

	Элементы.Группа6.Видимость = Объект.НедельныйГрафик;
	Элементы.Группа5.Видимость = Не Объект.НедельныйГрафик;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДнейНедели()

	ТекущийДень = НачалоНедели(ТекущаяДата()); // Начнем с понедельника

	Для Счетчик = 1 По 7 Цикл
		НазваниеДняНедели = Формат(ТекущийДень, "ДФ=дддд"); // Например, "среда"
		НазваниеДняНедели = ВРег(Лев(НазваниеДняНедели, 1)) + Сред(НазваниеДняНедели, 2); // Теперь, "Среда"

		СписокДнейНедели.Добавить(Строка(ДеньНедели(ТекущийДень)), НазваниеДняНедели);
		ТекущийДень = ТекущийДень + 86400; // Длина суток в секундах
	КонецЦикла;
	
	// Расставляем пометки для выходных дней графика
	Для Счетчик = 1 По СтрДлина(Объект.ВыходныеДни) Цикл
		СписокДнейНедели.НайтиПоЗначению(Сред(Объект.ВыходныеДни, Счетчик, 1)).Пометка = Истина;
	КонецЦикла;

КонецПроцедуры
&НаКлиенте
Процедура ВыходныеПриИзменении(Элемент)

	ЭлементСписка = Элемент.ТекущиеДанные;
	Если ЭлементСписка.Пометка Тогда
		Если Найти(Объект.ВыходныеДни, ЭлементСписка.Значение) = 0 Тогда
			Объект.ВыходныеДни = Объект.ВыходныеДни + ЭлементСписка.Значение;
		КонецЕсли;
	Иначе
		Объект.ВыходныеДни=СтрЗаменить(Объект.ВыходныеДни, ЭлементСписка.Значение, "");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)

	Если Объект.Ссылка.Пустая() Тогда
		ПоказатьПредупреждение( , "Перед заполнением график необходимо записать!", 10);
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(Интервал) = Ложь Тогда

		ДиалогСтандартногоПериода = Новый ДиалогРедактированияСтандартногоПериода;

		ДиалогСтандартногоПериода.Показать(
		Новый ОписаниеОповещения("ВыборПериодаЗавершение", ЭтотОбъект, ДиалогСтандартногоПериода, , ));

		Возврат;

	КонецЕсли;

	ЗаполнитьКалендарьЗаИнтервал();

КонецПроцедуры

Процедура ВыборПериодаЗавершение(Период, Параметры) Экспорт

	Если Период = Неопределено Тогда

		Возврат;
		
	КонецЕсли;

	Интервал = Период;

	ЗаполнитьКалендарьЗаИнтервал();

КонецПроцедуры
&НаСервере
Процедура ЗаполнитьКалендарьЗаИнтервал()

	ДокументОбъект = РеквизитФормыВЗначение("Объект");

	Если Объект.ВременнаяСхема.Количество() = 0 Тогда

		ДокументОбъект.ЗаполнитьКалендарьЗаИнтервал(Интервал);

	Иначе

		ДокументОбъект.ЗаполнитьКалендарь_ПоВременнойСхеме(Интервал);

	КонецЕсли;

	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");

КонецПроцедуры

&НаКлиенте
Процедура РабочаяНеделяПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

	Отказ = Истина;

КонецПроцедуры

&НаКлиенте
Процедура НедельныйГрафикПриИзменении(Элемент)
	Элементы.Группа6.Видимость = Объект.НедельныйГрафик;
	Элементы.Группа5.Видимость = Не Объект.НедельныйГрафик;

КонецПроцедуры