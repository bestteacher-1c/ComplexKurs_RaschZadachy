
&НаСервере
Процедура СформироватьНаСервере()
	
	ДгрГанта.Очистить();
	
	ДгрГанта.Обновление = Ложь;
	
	ДгрГанта.ОбластьЗаголовка.Текст = "Фактический период действия начислений";
	
	ДгрГанта.АвтоОпределениеПолногоИнтервала = Ложь;
	ДгрГанта.УстановитьПолныйИнтервал(Период.ДатаНачала,Период.ДатаОкончания);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОсновныеНачисленияФактическийПериодДействия.Регистратор,
	|	ОсновныеНачисленияФактическийПериодДействия.ФизическоеЛицо,
	|	ОсновныеНачисленияФактическийПериодДействия.ВидРасчета,
	|	ОсновныеНачисленияФактическийПериодДействия.ПериодДействияНачало,
	|	ОсновныеНачисленияФактическийПериодДействия.ПериодДействияКонец,
	|	ОсновныеНачисленияФактическийПериодДействия.Результат
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления.ФактическийПериодДействия(ПериодРегистрации МЕЖДУ &Дата1 И &Дата2) КАК
	|		ОсновныеНачисленияФактическийПериодДействия
	|УПОРЯДОЧИТЬ ПО
	|	ОсновныеНачисленияФактическийПериодДействия.ФизическоеЛицо.Код";
	
	Запрос.УстановитьПараметр("Дата1", Период.ДатаНачала );
	Запрос.УстановитьПараметр("Дата2", Период.ДатаОкончания);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекущийСотрудник = ДгрГанта.УстановитьТочку(Выборка.ФизическоеЛицо,Выборка.ФизическоеЛицо);
		ТекущийВидРасчета = ДгрГанта.УстановитьСерию(Выборка.ВидРасчета, Выборка.ВидРасчета);
		
		ТекущаяКоллекцияИнтервалов = ДгрГанта.ПолучитьЗначение(ТекущийСотрудник, ТекущийВидРасчета);
		
		ТекущийИнтервал = ТекущаяКоллекцияИнтервалов.Добавить();
		ТекущийИнтервал.Начало = Выборка.ПериодДействияНачало;
		ТекущийИнтервал.Конец = Выборка.ПериодДействияКонец;
		ТекущийИнтервал.Расшифровка = Выборка.Регистратор;
		ТекущийИнтервал.Текст = Выборка.Результат;
		
	КонецЦикла;
	
	
	ЭлементШкалыВремени = ДгрГанта.ОбластьПостроения.ШкалаВремени.Элементы[0];
	ЭлементШкалыВремени.Единица = ТипЕдиницыШкалыВремени.Месяц;
	ЭлементШкалыВремени.Кратность = 1;
	//ЭлементШкалыВремени.ФорматДня = ФорматДняШкалыВремени.ДеньМесяца;
	ЭлементШкалыВремени.Видимость = Истина;
	
	Если ДгрГанта.ОбластьПостроения.ШкалаВремени.Элементы.Количество() = 1 Тогда
	
		ЭлементШкалыВремени = ДгрГанта.ОбластьПостроения.ШкалаВремени.Элементы.Добавить();
		
		ЭлементШкалыВремени.Единица = ТипЕдиницыШкалыВремени.День;
		ЭлементШкалыВремени.Кратность = 1;
		ЭлементШкалыВремени.ФорматДня = ФорматДняШкалыВремени.ДеньМесяца;
		ЭлементШкалыВремени.Видимость = Истина;
		
	КонецЕсли;
	
	
	ДгрГанта.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.Период;
	
	ДгрГанта.Обновление = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		
		ДиалогСтандартногоПериода = Новый ДиалогРедактированияСтандартногоПериода();
		
		
		Если Не ДиалогСтандартногоПериода.Редактировать() Тогда 
			Возврат;
		КонецЕсли;
		
		Период = ДиалогСтандартногоПериода.Период;
		
	КонецЕсли;
	
	СформироватьНаСервере();

КонецПроцедуры
