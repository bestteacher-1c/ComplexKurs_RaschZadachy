#Область РасчетРесурсовРегистровРасчета

Процедура РассчитатьРесурсы_ОсновныеНачисления(НаборЗаписей) Экспорт 
	
	//НаборЗаписей = Движения.ОсновныеНачисления;
	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	
	//Получаем упорядоченные по категориям данные текущего набора записей, записанные в БД
	Менеджер = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
	|	ОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
	|	ОсновныеНачисления.Регистратор КАК Регистратор,
	|	ОсновныеНачисления.ПериодДействия КАК ПериодДействия,
	|	ОсновныеНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ОсновныеНачисления.ПериодДействияКонец КАК ПериодДействияКонец,
	|	ОсновныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ОсновныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	ОсновныеНачисления.Активность КАК Активность,
	|	ОсновныеНачисления.Сторно КАК Сторно,
	|	ОсновныеНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОсновныеНачисления.Подразделение КАК Подразделение,
	|	ОсновныеНачисления.Должность КАК Должность,
	|	ОсновныеНачисления.Результат КАК Результат,
	|	ОсновныеНачисления.ОтработаноДней КАК ОтработаноДней,
	|	ОсновныеНачисления.Размер КАК Размер,
	|	ОсновныеНачисления.ГрафикРаботы КАК ГрафикРаботы
	|ПОМЕСТИТЬ ВТДвижения
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.Регистратор = &Регистратор
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТДвижения.ВидРасчета
	|ПОМЕСТИТЬ ВТВидыРасчетаДокумента
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВидыРасчетаДокумента.ВидРасчета,
	|	ОсновныеНачисленияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета
	|ПОМЕСТИТЬ ВТВедущиеВидыРасчета
	|ИЗ
	|	ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисления.ВедущиеВидыРасчета КАК ОсновныеНачисленияВедущиеВидыРасчета
	|		ПО ВТВидыРасчетаДокумента.ВидРасчета = ОсновныеНачисленияВедущиеВидыРасчета.Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВедущиеВидыРасчета.ВидРасчета,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТВидыРасчетаДокумента.ВидРасчета) КАК Приоритет
	|ПОМЕСТИТЬ ВТПриоритетыВидовРасчета
	|ИЗ
	|	ВТВедущиеВидыРасчета КАК ВТВедущиеВидыРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ПО ВТВедущиеВидыРасчета.ВедущийВидРасчета = ВТВидыРасчетаДокумента.ВидРасчета
	|СГРУППИРОВАТЬ ПО
	|	ВТВедущиеВидыРасчета.ВидРасчета
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвижения.НомерСтроки КАК НомерСтроки,
	|	ВТДвижения.ВидРасчета КАК ВидРасчета,
	|	ВТДвижения.ПериодРегистрации КАК ПериодРегистрации,
	|	ВТДвижения.Регистратор КАК Регистратор,
	|	ВТДвижения.ПериодДействия КАК ПериодДействия,
	|	ВТДвижения.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ВТДвижения.ПериодДействияКонец КАК ПериодДействияКонец,
	|	ВТДвижения.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ВТДвижения.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	ВТДвижения.Активность КАК Активность,
	|	ВТДвижения.Сторно КАК Сторно,
	|	ВТДвижения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТДвижения.Подразделение КАК Подразделение,
	|	ВТДвижения.Должность КАК Должность,
	|	ВТДвижения.Результат КАК Результат,
	|	ВТДвижения.ОтработаноДней КАК ОтработаноДней,
	|	ВТДвижения.Размер КАК Размер,
	|	ВТДвижения.ГрафикРаботы КАК ГрафикРаботы,
	|	ВТПриоритетыВидовРасчета.Приоритет * выбор
	|		когда сторно
	|			тогда -1
	|		иначе 1
	|	конец КАК Приоритет
	|ПОМЕСТИТЬ ВТНаборЗаписей
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритетыВидовРасчета КАК ВТПриоритетыВидовРасчета
	|		ПО ВТДвижения.ВидРасчета = ВТПриоритетыВидовРасчета.ВидРасчета
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки,
	|	ВТНаборЗаписей.Приоритет КАК Приоритет,
	|	ВТНаборЗаписей.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	ВТНаборЗаписей.ВидРасчета.Формула КАК Формула,
	|	ВТНаборЗаписей.ВидРасчета.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени
	|ИЗ
	|	ВТНаборЗаписей КАК ВТНаборЗаписей
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	НомерСтроки
	|ИТОГИ
	|ПО
	|	Приоритет
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДвижения
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВидыРасчетаДокумента
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВедущиеВидыРасчета
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПриоритетыВидовРасчета";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПриоритет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Внешний цикл по категориям
	Пока ВыборкаПриоритет.Следующий() Цикл
		
		//Запрос для получения базы и данных графика для записей с текущей категорией 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТЗаписиДляРасчета
		|ИЗ
		|	ВТНаборЗаписей КАК ВТНаборЗаписей
		|ГДЕ
		|	ВТНаборЗаписей.Приоритет = &Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ЗначениеДниФактическийПериодДействия,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеДниПериодДействия, 0) КАК ЗначениеДниПериодДействия
		|ПОМЕСТИТЬ ВТДанныеГрафика
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетДанныеГрафика = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК ОсновныеНачисленияДанныеГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза КАК РезультатБаза,
		|	ОсновныеНачисленияБазаОсновныеНачисления.ОтработаноДнейБаза КАК ОтработаноДнейБаза
		|ПОМЕСТИТЬ ВТБазаВОсновныхНачислениях
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
		|			&Измерения_ОсновныеНачисления,
		|			&Измерения_ОсновныеНачисления,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК ОсновныеНачисленияБазаОсновныеНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗаписиДляРасчета.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ЗначениеДниФактическийПериодДействия,
		|	ЕСТЬNULL(ВТДанныеГрафика.ЗначениеДниПериодДействия, 0) КАК ЗначениеДниПериодДействия,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза
		|ИЗ
		|	ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеГрафика КАК ВТДанныеГрафика
		|		ПО (ВТДанныеГрафика.НомерСтроки = ВТЗаписиДляРасчета.НомерСтроки)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВОсновныхНачислениях КАК ВТБазаВОсновныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВОсновныхНачислениях.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТЗаписиДляРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДанныеГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВОсновныхНачислениях";
		Измерения_ОсновныеНачисления = Новый Массив;
		Измерения_ОсновныеНачисления.Добавить("ФизическоеЛицо");
		Измерения_ОсновныеНачисления.Добавить("Подразделение");
		Измерения_ОсновныеНачисления.Добавить("Должность");

		Запрос.УстановитьПараметр("Приоритет", ВыборкаПриоритет.Приоритет);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		Запрос.УстановитьПараметр("Измерения_ОсновныеНачисления", Измерения_ОсновныеНачисления);

		РезультатЗапроса = Запрос.Выполнить();
		
		//Выборка с данными для записей текущей категории
		Выборка_ДанныеГрафика_База = РезультатЗапроса.Выбрать();
		
		
		//Вложенный цикл по записям текущей категории
		ВыборкаДетальныеЗаписи = ВыборкаПриоритет.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Запись = НаборЗаписей.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);

			Выборка_ДанныеГрафика_База.НайтиСледующий(ВыборкаДетальныеЗаписи.НомерСтроки, "НомерСтроки");

			РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись);
			
			//Проверка - надо ли записать отработанные дни
			Если ВыборкаДетальныеЗаписи.ЗачетОтработанногоВремени Тогда

				Запись.ОтработаноДней = Выборка_ДанныеГрафика_База.ЗначениеДниФактическийПериодДействия;

			КонецЕсли;

			Если (Запись.Сторно = Истина) Тогда
				Запись.ОтработаноДней = -1 * Запись.ОтработаноДней;
			КонецЕсли;
		КонецЦикла;

		НаборЗаписей.Записать( , Истина);

	КонецЦикла;
КонецПроцедуры

Процедура РассчитатьРесурсы_ДополнительныеНачисления(НаборЗаписей) Экспорт
	
	//НаборЗаписей = Движения.ДополнительныеНачисления;
	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	
	
	//Получаем упорядоченные по категориям данные текущего набора записей, записанные в БД
	Менеджер = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
				   |	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
				   |	ДополнительныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
				   |	ДополнительныеНачисления.Регистратор КАК Регистратор,
				   |	ДополнительныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
				   |	ДополнительныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
				   |	ДополнительныеНачисления.Активность КАК Активность,
				   |	ДополнительныеНачисления.Сторно КАК Сторно,
				   |	ДополнительныеНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
				   |	ДополнительныеНачисления.Подразделение КАК Подразделение,
				   |	ДополнительныеНачисления.Должность КАК Должность,
				   |	ДополнительныеНачисления.Результат КАК Результат,
				   |	ДополнительныеНачисления.Размер КАК Размер
				   |ПОМЕСТИТЬ ВТДвижения
				   |ИЗ
				   |	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
				   |ГДЕ
				   |	ДополнительныеНачисления.Регистратор = &Регистратор
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета
				   |ПОМЕСТИТЬ ВТВидыРасчетаДокумента
				   |ИЗ
				   |	ВТДвижения КАК ДополнительныеНачисления
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТВидыРасчетаДокумента.ВидРасчета КАК ВидРасчета,
				   |	ДополнительныеНачисленияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета
				   |ПОМЕСТИТЬ ВТВедущиеВидыРасчета
				   |ИЗ
				   |	ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
				   |		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ДополнительныеНачисления.ВедущиеВидыРасчета КАК ДополнительныеНачисленияВедущиеВидыРасчета
				   |		ПО ВТВидыРасчетаДокумента.ВидРасчета = ДополнительныеНачисленияВедущиеВидыРасчета.Ссылка
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТВедущиеВидыРасчета.ВидРасчета КАК ВидРасчета,
				   |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТВидыРасчетаДокумента.ВидРасчета) КАК Приоритет
				   |ПОМЕСТИТЬ ВТПриоритетыВидовРасчета
				   |ИЗ
				   |	ВТВедущиеВидыРасчета КАК ВТВедущиеВидыРасчета
				   |		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
				   |		ПО ВТВедущиеВидыРасчета.ВедущийВидРасчета = ВТВидыРасчетаДокумента.ВидРасчета
				   |
				   |СГРУППИРОВАТЬ ПО
				   |	ВТВедущиеВидыРасчета.ВидРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТПриоритетыВидовРасчета.Приоритет КАК Приоритет,
				   |	ВТДвижения.НомерСтроки КАК НомерСтроки,
				   |	ВТДвижения.ВидРасчета КАК ВидРасчета,
				   |	ВТДвижения.ПериодРегистрации КАК ПериодРегистрации,
				   |	ВТДвижения.Регистратор КАК Регистратор,
				   |	ВТДвижения.БазовыйПериодНачало КАК БазовыйПериодНачало,
				   |	ВТДвижения.БазовыйПериодКонец КАК БазовыйПериодКонец,
				   |	ВТДвижения.Активность КАК Активность,
				   |	ВТДвижения.Сторно КАК Сторно,
				   |	ВТДвижения.ФизическоеЛицо,
				   |	ВТДвижения.Подразделение КАК Подразделение,
				   |	ВТДвижения.Должность КАК Должность,
				   |	ВТДвижения.Результат КАК Результат,
				   |	ВТДвижения.Размер КАК Размер
				   |ПОМЕСТИТЬ ВТНаборЗаписей
				   |ИЗ
				   |	ВТДвижения КАК ВТДвижения
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритетыВидовРасчета КАК ВТПриоритетыВидовРасчета
				   |		ПО ВТДвижения.ВидРасчета = ВТПриоритетыВидовРасчета.ВидРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки,
				   |	ВТНаборЗаписей.ВидРасчета КАК ВидРасчета,
				   |	ВТНаборЗаписей.ВидРасчета.СпособРасчета КАК СпособРасчета,
				   |	ВТНаборЗаписей.Приоритет КАК Приоритет
				   |ИЗ
				   |	ВТНаборЗаписей КАК ВТНаборЗаписей
				   |
				   |УПОРЯДОЧИТЬ ПО
				   |	Приоритет,
				   |	НомерСтроки
				   |ИТОГИ ПО
				   |	Приоритет
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТДвижения
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТВидыРасчетаДокумента
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТВедущиеВидыРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТПриоритетыВидовРасчета";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПриоритет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Внешний цикл по категориям
	Пока ВыборкаПриоритет.Следующий() Цикл
		
		//Запрос для получения базы и данных графика для записей с текущей категорией 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТЗаписиДляРасчета
		|ИЗ
		|	ВТНаборЗаписей КАК ВТНаборЗаписей
		|ГДЕ
		|	ВТНаборЗаписей.Приоритет = &Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.ОтработаноДнейБаза
		|ПОМЕСТИТЬ ВТБазаВОсновныхНачислениях
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
		|			&ИЗмерения_ДополнительныеНачисления,
		|			&Измерения_ОсновныеНачисления,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК ДополнительныеНачисленияБазаОсновныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеНачисленияБазаДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисленияБазаДополнительныеНачисления.РезультатБаза
		|ПОМЕСТИТЬ ВТБазаВДополнительныхНачислениях
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления.БазаДополнительныеНачисления(
		|			&ИЗмерения_ДополнительныеНачисления,
		|			&ИЗмерения_ДополнительныеНачисления,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК ДополнительныеНачисленияБазаДополнительныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗаписиДляРасчета.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.РезультатБаза, 0) + ЕСТЬNULL(ВТБазаВДополнительныхНачислениях.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза
		|ИЗ
		|	ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВОсновныхНачислениях КАК ВТБазаВОсновныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВОсновныхНачислениях.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВДополнительныхНачислениях КАК ВТБазаВДополнительныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВДополнительныхНачислениях.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВОсновныхНачислениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВДополнительныхНачислениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТЗаписиДляРасчета";
		Измерения_ДополнительныеНачисления = Новый Массив;
		Измерения_ДополнительныеНачисления.Добавить("ФизическоеЛицо");
		Измерения_ДополнительныеНачисления.Добавить("Подразделение");
		Измерения_ДополнительныеНачисления.Добавить("Должность");

		Измерения_ОсновныеНачисления = Новый Массив;
		Измерения_ОсновныеНачисления.Добавить("ФизическоеЛицо");
		Измерения_ОсновныеНачисления.Добавить("Подразделение");
		Измерения_ОсновныеНачисления.Добавить("Должность");

		Запрос.УстановитьПараметр("Измерения_ДополнительныеНачисления", Измерения_ДополнительныеНачисления);
		Запрос.УстановитьПараметр("Измерения_ОсновныеНачисления", Измерения_ОсновныеНачисления);
		Запрос.УстановитьПараметр("Приоритет", ВыборкаПриоритет.Приоритет);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);

		РезультатЗапроса = Запрос.Выполнить();
		
		//Выборка с данными для записей текущей категории
		Выборка_ДанныеГрафика_База = РезультатЗапроса.Выбрать();
		
		
		//Вложенный цикл по записям текущей категории
		ВыборкаДетальныеЗаписи = ВыборкаПриоритет.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Запись = НаборЗаписей.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);

			Выборка_ДанныеГрафика_База.НайтиСледующий(ВыборкаДетальныеЗаписи.НомерСтроки, "НомерСтроки");

			РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись);

		КонецЦикла;

		НаборЗаписей.Записать( , Истина);

	КонецЦикла;

КонецПроцедуры

Процедура РассчитатьРесурсы_Удержания(НаборЗаписей) Экспорт

	//НаборЗаписей = Движения.Удержания;
	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	
    //Получаем упорядоченные по категориям данные текущего набора записей, записанные в БД
	Менеджер = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Удержания.НомерСтроки КАК НомерСтроки,
	|	Удержания.ВидРасчета КАК ВидРасчета,
	|	Удержания.ПериодРегистрации КАК ПериодРегистрации,
	|	Удержания.Регистратор КАК Регистратор,
	|	Удержания.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	Удержания.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	Удержания.Активность КАК Активность,
	|	Удержания.Сторно КАК Сторно,
	|	Удержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Удержания.Подразделение КАК Подразделение,
	|	Удержания.Должность КАК Должность,
	|	Удержания.Результат КАК Результат,
	|	Удержания.Размер КАК Размер
	|ПОМЕСТИТЬ ВТДвижения
	|ИЗ
	|	РегистрРасчета.Удержания КАК Удержания
	|ГДЕ
	|	Удержания.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТДвижения.ВидРасчета КАК ВидРасчета
	|ПОМЕСТИТЬ ВТВидыРасчетаДокумента
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВидыРасчетаДокумента.ВидРасчета КАК ВидРасчета,
	|	УдержанияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета
	|ПОМЕСТИТЬ ВТВедущиеВидыРасчета
	|ИЗ
	|	ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания.ВедущиеВидыРасчета КАК УдержанияВедущиеВидыРасчета
	|		ПО ВТВидыРасчетаДокумента.ВидРасчета = УдержанияВедущиеВидыРасчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВедущиеВидыРасчета.ВидРасчета КАК ВидРасчета,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТВидыРасчетаДокумента.ВидРасчета) КАК Приоритет
	|ПОМЕСТИТЬ ВТПриоритетыВидовРасчета
	|ИЗ
	|	ВТВедущиеВидыРасчета КАК ВТВедущиеВидыРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ПО ВТВедущиеВидыРасчета.ВедущийВидРасчета = ВТВидыРасчетаДокумента.ВидРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВедущиеВидыРасчета.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвижения.НомерСтроки КАК НомерСтроки,
	|	ВТДвижения.ВидРасчета КАК ВидРасчета,
	|	ВТДвижения.ПериодРегистрации КАК ПериодРегистрации,
	|	ВТДвижения.Регистратор КАК Регистратор,
	|	ВТДвижения.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ВТДвижения.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	ВТДвижения.Активность КАК Активность,
	|	ВТДвижения.Сторно КАК Сторно,
	|	ВТДвижения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТДвижения.Подразделение КАК Подразделение,
	|	ВТДвижения.Должность КАК Должность,
	|	ВТДвижения.Результат КАК Результат,
	|	ВТДвижения.Размер КАК Размер,
	|	ВТПриоритетыВидовРасчета.Приоритет КАК Приоритет
	|ПОМЕСТИТЬ ВТНаборЗаписей
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритетыВидовРасчета КАК ВТПриоритетыВидовРасчета
	|		ПО ВТДвижения.ВидРасчета = ВТПриоритетыВидовРасчета.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки,
	|	ВТНаборЗаписей.ВидРасчета КАК ВидРасчета,
	|	ВТНаборЗаписей.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	ВТНаборЗаписей.Приоритет КАК Приоритет
	|ИЗ
	|	ВТНаборЗаписей КАК ВТНаборЗаписей
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВидыРасчетаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВедущиеВидыРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПриоритетыВидовРасчета";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПриоритет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Внешний цикл по категориям
	Пока ВыборкаПриоритет.Следующий() Цикл
		
		//Запрос для получения базы и данных графика для записей с текущей категорией 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТЗаписиДляРасчета
		|ИЗ
		|	ВТНаборЗаписей КАК ВТНаборЗаписей
		|ГДЕ
		|	ВТНаборЗаписей.Приоритет = &Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УдержанияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	УдержанияБазаОсновныеНачисления.РезультатБаза КАК РезультатБаза,
		|	УдержанияБазаОсновныеНачисления.ОтработаноДнейБаза КАК ОтработаноДнейБаза
		|ПОМЕСТИТЬ ВТБазаВОсновныхНачислениях
		|ИЗ
		|	РегистрРасчета.Удержания.БазаОсновныеНачисления(
		|			&ИЗмерения_Удержания,
		|			&Измерения_ОсновныеНачисления,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК УдержанияБазаОсновныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УдержанияБазаДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	УдержанияБазаДополнительныеНачисления.РезультатБаза КАК РезультатБаза
		|ПОМЕСТИТЬ ВТБазаВДополнительныхНачислениях
		|ИЗ
		|	РегистрРасчета.Удержания.БазаДополнительныеНачисления(
		|			&ИЗмерения_Удержания,
		|			&ИЗмерения_ДополнительныеНачисления,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК УдержанияБазаДополнительныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗаписиДляРасчета.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.РезультатБаза, 0) + ЕСТЬNULL(ВТБазаВДополнительныхНачислениях.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза
		|ИЗ
		|	ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВОсновныхНачислениях КАК ВТБазаВОсновныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВОсновныхНачислениях.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВДополнительныхНачислениях КАК ВТБазаВДополнительныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВДополнительныхНачислениях.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВОсновныхНачислениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВДополнительныхНачислениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТЗаписиДляРасчета";

		Измерения_ДополнительныеНачисления = Новый Массив;
		Измерения_ДополнительныеНачисления.Добавить("ФизическоеЛицо");
		Измерения_ДополнительныеНачисления.Добавить("Подразделение");
		Измерения_ДополнительныеНачисления.Добавить("Должность");

		Измерения_ОсновныеНачисления = Новый Массив;
		Измерения_ОсновныеНачисления.Добавить("ФизическоеЛицо");
		Измерения_ОсновныеНачисления.Добавить("Подразделение");
		Измерения_ОсновныеНачисления.Добавить("Должность");

		Измерения_Удержания = Новый Массив;
		Измерения_Удержания.Добавить("ФизическоеЛицо");
		Измерения_Удержания.Добавить("Подразделение");
		Измерения_Удержания.Добавить("Должность");

		Запрос.УстановитьПараметр("Измерения_ДополнительныеНачисления", Измерения_ДополнительныеНачисления);
		Запрос.УстановитьПараметр("Измерения_ОсновныеНачисления", Измерения_ОсновныеНачисления);
		Запрос.УстановитьПараметр("Измерения_Удержания", Измерения_Удержания);
		Запрос.УстановитьПараметр("Приоритет", ВыборкаПриоритет.Приоритет);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);

		РезультатЗапроса = Запрос.Выполнить();
		
		//Выборка с данными для записей текущей категории
		Выборка_ДанныеГрафика_База = РезультатЗапроса.Выбрать();
		
		
		//Вложенный цикл по записям текущей категории
		ВыборкаДетальныеЗаписи = ВыборкаПриоритет.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Запись = НаборЗаписей.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);

			Выборка_ДанныеГрафика_База.НайтиСледующий(ВыборкаДетальныеЗаписи.НомерСтроки, "НомерСтроки");

			РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись);

		КонецЦикла;

		НаборЗаписей.Записать( , Истина);

	КонецЦикла;

КонецПроцедуры

Процедура РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись)
	
	//1
	Если ВыборкаДетальныеЗаписи.СпособРасчета = Перечисления.СпособыРасчета.ПоМесячнойТарифнойСтавкеПоДням Тогда

		Норма = Выборка_ДанныеГрафика_База.ЗначениеДниПериодДействия;

		Если Норма = 0 Тогда

			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнен график!";
			Сообщение.Сообщить();

			Возврат;

		КонецЕсли;

		Факт = Выборка_ДанныеГрафика_База.ЗначениеДниФактическийПериодДействия;

		Ставка = Запись.Размер;

		Запись.Результат = Ставка * Факт / Норма;
		
	//2
	ИначеЕсли ВыборкаДетальныеЗаписи.СпособРасчета = Перечисления.СпособыРасчета.Процентом Тогда

		База = Выборка_ДанныеГрафика_База.РезультатБаза;

		Процент = Запись.Размер;

		Запись.Результат = База * Процент / 100;
		
	//3	
	ИначеЕсли ВыборкаДетальныеЗаписи.СпособРасчета = Перечисления.СпособыРасчета.НулеваяСумма Тогда

		Запись.Результат = 0;
		
	//4	
	ИначеЕсли ВыборкаДетальныеЗаписи.СпособРасчета = Перечисления.СпособыРасчета.ПоСреднемуЗаработку Тогда

		ОтработалЗаТриМесяца = Выборка_ДанныеГрафика_База.ОтработаноДнейБаза;

		ДнейБолел = Выборка_ДанныеГрафика_База.ЗначениеДниФактическийПериодДействия;

		Если ОтработалЗаТриМесяца = 0 Тогда
			
			//У нас социальное государство. Ст. 7 Конституции РФ.
			//Вы не работали до заболевания и нет базы для расчета среднего заработка.
			//Значит больничный платиться из расчтета по МРОТ (для примера взят МРОТ на 01.05.2016 г.)

			Запись.Результат = 7800 / Выборка_ДанныеГрафика_База.ЗначениеДниПериодДействия * ДнейБолел; 
			
			//Кстати. Ст. 29 Каждому гарантируется свобода мысли и слова.
			
			//Ст. 31. Граждане Российской Федерации имеют право собираться мирно,
			//без оружия, проводить собрания, митинги и демонстрации, шествия и пикетирование.

			Возврат;
		КонецЕсли;

		ЗаработалЗаТриМесяца = Выборка_ДанныеГрафика_База.РезультатБаза;

		ПроцентВыплаты = Запись.Размер;

		Запись.Результат = (ЗаработалЗаТриМесяца / ОтработалЗаТриМесяца) * ДнейБолел * ПроцентВыплаты / 100;
	//5	
	ИначеЕсли ВыборкаДетальныеЗаписи.СпособРасчета = Перечисления.СпособыРасчета.КомпенсацияЗаДень Тогда

		ДнейНаВыезде = Выборка_ДанныеГрафика_База.ОтработаноДнейБаза;
		СуммаЗаДень = Запись.Размер;

		Запись.Результат = ДнейНаВыезде * СуммаЗаДень;
	//6	
	ИначеЕсли ВыборкаДетальныеЗаписи.СпособРасчета = Перечисления.СпособыРасчета.ФиксированнойСуммой Тогда

		Запись.Результат = Запись.Размер;
	//7	
	ИначеЕсли ВыборкаДетальныеЗаписи.СпособРасчета = Перечисления.СпособыРасчета.ПоФормуле Тогда

		Выполнить ("Запись.Результат = " + ВыборкаДетальныеЗаписи.Формула + "");

	КонецЕсли;

	Если (Запись.Сторно = Истина) Тогда
		Запись.Результат = -1 * Запись.Результат;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область Взаиморасчеты
Процедура ЗаполнитьНаборЗаписей_ВзаиморасчетыСРаботниками(НаборЗаписей) Экспорт

	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	//НаборЗаписей = Движения.ВзаиморасчетыСРаботниками;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОсновныеНачисления.ФизическоеЛицо,
	|	ОсновныеНачисления.Результат
	|ПОМЕСТИТЬ ВТНачислено
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.Регистратор = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДополнительныеНачисления.ФизическоеЛицо,
	|	ДополнительныеНачисления.Результат
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
	|ГДЕ
	|	ДополнительныеНачисления.Регистратор = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Удержания.ФизическоеЛицо,
	|	-Удержания.Результат
	|ИЗ
	|	РегистрРасчета.Удержания КАК Удержания
	|ГДЕ
	|	Удержания.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНачислено.ФизическоеЛицо КАК Работник,
	|	СУММА(ВТНачислено.Результат) КАК Сумма
	|ИЗ
	|	ВТНачислено КАК ВТНачислено
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТНачислено.ФизическоеЛицо";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл

		Движение = НаборЗаписей.Добавить();

		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Ссылка.Дата;
		Движение.Работник = Выборка.Работник;
		Движение.Сумма = Выборка.Сумма;

	КонецЦикла;

	НаборЗаписей.Записывать = Истина;

КонецПроцедуры

#КонецОбласти
#Область Перерасчеты

Процедура ПереРассчитатьРесурсы_ОсновныеНачисления(НаборЗаписей) Экспорт 
	
	//НаборЗаписей = Движения.ОсновныеНачисления;
	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	
	//Получаем упорядоченные по категориям данные текущего набора записей, записанные в БД
	Менеджер = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Перерасчет.ВидРасчета КАК ВидРасчета,
	|	Перерасчет.Работник КАК Работник
	|ПОМЕСТИТЬ ВТПерерасчет
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления.Перерасчет КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
	|	ОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
	|	ОсновныеНачисления.Регистратор КАК Регистратор,
	|	ОсновныеНачисления.ПериодДействия КАК ПериодДействия,
	|	ОсновныеНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ОсновныеНачисления.ПериодДействияКонец КАК ПериодДействияКонец,
	|	ОсновныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ОсновныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	ОсновныеНачисления.Активность КАК Активность,
	|	ОсновныеНачисления.Сторно КАК Сторно,
	|	ОсновныеНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОсновныеНачисления.Подразделение КАК Подразделение,
	|	ОсновныеНачисления.Должность КАК Должность,
	|	ОсновныеНачисления.Результат КАК Результат,
	|	ОсновныеНачисления.ОтработаноДней КАК ОтработаноДней,
	|	ОсновныеНачисления.Размер КАК Размер,
	|	ОсновныеНачисления.ГрафикРаботы КАК ГрафикРаботы
	|ПОМЕСТИТЬ ВТДвижения
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПерерасчет КАК ВТПерерасчет
	|		ПО (ВТПерерасчет.ВидРасчета = ОсновныеНачисления.ВидРасчета)
	|			И (ВТПерерасчет.Работник = ОсновныеНачисления.ФизическоеЛицо)
	|ГДЕ
	|	ОсновныеНачисления.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОсновныеНачисления.ВидРасчета
	|ПОМЕСТИТЬ ВТВидыРасчетаДокумента
	|ИЗ
	|	ВТДвижения КАК ОсновныеНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВидыРасчетаДокумента.ВидРасчета,
	|	ОсновныеНачисленияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета
	|ПОМЕСТИТЬ ВТВедущиеВидыРасчета
	|ИЗ
	|	ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисления.ВедущиеВидыРасчета КАК ОсновныеНачисленияВедущиеВидыРасчета
	|		ПО ВТВидыРасчетаДокумента.ВидРасчета = ОсновныеНачисленияВедущиеВидыРасчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВедущиеВидыРасчета.ВидРасчета КАК ВидРасчета,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТВидыРасчетаДокумента.ВидРасчета) КАК Приоритет
	|ПОМЕСТИТЬ ВТПриоритетыВидовРасчета
	|ИЗ
	|	ВТВедущиеВидыРасчета КАК ВТВедущиеВидыРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ПО ВТВедущиеВидыРасчета.ВедущийВидРасчета = ВТВидыРасчетаДокумента.ВидРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВедущиеВидыРасчета.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвижения.НомерСтроки КАК НомерСтроки,
	|	ВТДвижения.ВидРасчета КАК ВидРасчета,
	|	ВТДвижения.ПериодРегистрации КАК ПериодРегистрации,
	|	ВТДвижения.Регистратор КАК Регистратор,
	|	ВТДвижения.ПериодДействия КАК ПериодДействия,
	|	ВТДвижения.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ВТДвижения.ПериодДействияКонец КАК ПериодДействияКонец,
	|	ВТДвижения.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ВТДвижения.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	ВТДвижения.Активность КАК Активность,
	|	ВТДвижения.Сторно КАК Сторно,
	|	ВТДвижения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТДвижения.Подразделение КАК Подразделение,
	|	ВТДвижения.Должность КАК Должность,
	|	ВТДвижения.Результат КАК Результат,
	|	ВТДвижения.ОтработаноДней КАК ОтработаноДней,
	|	ВТДвижения.Размер КАК Размер,
	|	ВТДвижения.ГрафикРаботы КАК ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА ВТДвижения.Сторно
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ВТПриоритетыВидовРасчета.Приоритет КАК Приоритет
	|ПОМЕСТИТЬ ВТНаборЗаписей
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритетыВидовРасчета КАК ВТПриоритетыВидовРасчета
	|		ПО ВТДвижения.ВидРасчета = ВТПриоритетыВидовРасчета.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки,
	|	ВТНаборЗаписей.ВидРасчета,
	|	ВТНаборЗаписей.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	ВТНаборЗаписей.Приоритет КАК Приоритет,
	|	ВТНаборЗаписей.ВидРасчета.Формула КАК Формула,
	|	ВТНаборЗаписей.ВидРасчета.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени
	|ИЗ
	|	ВТНаборЗаписей КАК ВТНаборЗаписей
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВидыРасчетаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВедущиеВидыРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПриоритетыВидовРасчета";
	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПриоритет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Внешний цикл по категориям
	Пока ВыборкаПриоритет.Следующий() Цикл
		
		//Запрос для получения базы и данных графика для записей с текущей категорией 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТЗаписиДляРасчета
		|ИЗ
		|	ВТНаборЗаписей КАК ВТНаборЗаписей
		|ГДЕ
		|	ВТНаборЗаписей.Приоритет = &Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ЗначениеДниФактическийПериодДействия,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеДниПериодДействия, 0) КАК ЗначениеДниПериодДействия
		|ПОМЕСТИТЬ ВТДанныеГрафика
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетДанныеГрафика = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК ОсновныеНачисленияДанныеГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза КАК РезультатБаза,
		|	ОсновныеНачисленияБазаОсновныеНачисления.ОтработаноДнейБаза КАК ОтработаноДнейБаза
		|ПОМЕСТИТЬ ВТБазаВОсновныхНачислениях
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
		|			&Измерения_ОсновныеНачисления,
		|			&Измерения_ОсновныеНачисления,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК ОсновныеНачисленияБазаОсновныеНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗаписиДляРасчета.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ЗначениеДниФактическийПериодДействия,
		|	ЕСТЬNULL(ВТДанныеГрафика.ЗначениеДниПериодДействия, 0) КАК ЗначениеДниПериодДействия,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза
		|ИЗ
		|	ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеГрафика КАК ВТДанныеГрафика
		|		ПО (ВТДанныеГрафика.НомерСтроки = ВТЗаписиДляРасчета.НомерСтроки)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВОсновныхНачислениях КАК ВТБазаВОсновныхНачислениях
		|		ПО (ВТБазаВОсновныхНачислениях.НомерСтроки = ВТЗаписиДляРасчета.НомерСтроки)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТЗаписиДляРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДанныеГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВОсновныхНачислениях";
		Измерения_ОсновныеНачисления = Новый Массив;
		Измерения_ОсновныеНачисления.Добавить("ФизическоеЛицо");
		Измерения_ОсновныеНачисления.Добавить("Подразделение");
		Измерения_ОсновныеНачисления.Добавить("Должность");

		Запрос.УстановитьПараметр("Приоритет", ВыборкаПриоритет.Приоритет);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		Запрос.УстановитьПараметр("Измерения_ОсновныеНачисления", Измерения_ОсновныеНачисления);

		РезультатЗапроса = Запрос.Выполнить();
		
		//Выборка с данными для записей текущей категории
		Выборка_ДанныеГрафика_База = РезультатЗапроса.Выбрать();
		
		
		//Вложенный цикл по записям текущей категории
		ВыборкаДетальныеЗаписи = ВыборкаПриоритет.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Запись = НаборЗаписей.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);

			Выборка_ДанныеГрафика_База.НайтиСледующий(ВыборкаДетальныеЗаписи.НомерСтроки, "НомерСтроки");

			РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись);
			
			//Проверка - надо ли записать отработанные дни
			Если ВыборкаДетальныеЗаписи.ЗачетОтработанногоВремени Тогда

				Запись.ОтработаноДней = Выборка_ДанныеГрафика_База.ЗначениеДниФактическийПериодДействия;

			КонецЕсли;
			
			Если (Запись.Сторно = Истина) Тогда
				Запись.ОтработаноДней = -1 * Запись.ОтработаноДней;
			КонецЕсли;

		КонецЦикла;

		НаборЗаписей.Записать( , , Ложь, Ложь);

	КонецЦикла;

	НаборЗаписей.Записать( , , Ложь, Истина);

КонецПроцедуры

Процедура ПереРассчитатьРесурсы_ДополнительныеНачисления(НаборЗаписей) Экспорт
	
	//НаборЗаписей = Движения.ДополнительныеНачисления;
	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	
	
	//Получаем упорядоченные по категориям данные текущего набора записей, записанные в БД
	Менеджер = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст = "ВЫБРАТЬ
				   |	Перерасчет.ВидРасчета КАК ВидРасчета,
				   |	Перерасчет.Работник КАК Работник
				   |ПОМЕСТИТЬ ВТПерерасчет
				   |ИЗ
				   |	РегистрРасчета.ДополнительныеНачисления.Перерасчет КАК Перерасчет
				   |ГДЕ
				   |	Перерасчет.ОбъектПерерасчета = &Регистратор
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
				   |	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
				   |	ДополнительныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
				   |	ДополнительныеНачисления.Регистратор КАК Регистратор,
				   |	ДополнительныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
				   |	ДополнительныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
				   |	ДополнительныеНачисления.Активность КАК Активность,
				   |	ДополнительныеНачисления.Сторно КАК Сторно,
				   |	ДополнительныеНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
				   |	ДополнительныеНачисления.Подразделение КАК Подразделение,
				   |	ДополнительныеНачисления.Должность КАК Должность,
				   |	ДополнительныеНачисления.Результат КАК Результат,
				   |	ДополнительныеНачисления.Размер КАК Размер
				   |ПОМЕСТИТЬ ВТДвижения
				   |ИЗ
				   |	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПерерасчет КАК ВТПерерасчет
				   |		ПО ДополнительныеНачисления.ФизическоеЛицо = ВТПерерасчет.Работник
				   |			И ДополнительныеНачисления.ВидРасчета = ВТПерерасчет.ВидРасчета
				   |ГДЕ
				   |	ДополнительныеНачисления.Регистратор = &Регистратор
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета
				   |ПОМЕСТИТЬ ВТВидыРасчетаДокумента
				   |ИЗ
				   |	ВТДвижения КАК ДополнительныеНачисления
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТВидыРасчетаДокумента.ВидРасчета КАК ВидРасчета,
				   |	ДополнительныеНачисленияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета
				   |ПОМЕСТИТЬ ВТВедущиеВидыРасчета
				   |ИЗ
				   |	ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
				   |		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ДополнительныеНачисления.ВедущиеВидыРасчета КАК ДополнительныеНачисленияВедущиеВидыРасчета
				   |		ПО ВТВидыРасчетаДокумента.ВидРасчета = ДополнительныеНачисленияВедущиеВидыРасчета.Ссылка
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТВедущиеВидыРасчета.ВидРасчета КАК ВидРасчета,
				   |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТВидыРасчетаДокумента.ВидРасчета) КАК Приоритет
				   |ПОМЕСТИТЬ ВТПриоритетыВидовРасчета
				   |ИЗ
				   |	ВТВедущиеВидыРасчета КАК ВТВедущиеВидыРасчета
				   |		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
				   |		ПО ВТВедущиеВидыРасчета.ВедущийВидРасчета = ВТВидыРасчетаДокумента.ВидРасчета
				   |
				   |СГРУППИРОВАТЬ ПО
				   |	ВТВедущиеВидыРасчета.ВидРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТДвижения.НомерСтроки КАК НомерСтроки,
				   |	ВТДвижения.ВидРасчета КАК ВидРасчета,
				   |	ВТДвижения.ПериодРегистрации КАК ПериодРегистрации,
				   |	ВТДвижения.Регистратор КАК Регистратор,
				   |	ВТДвижения.БазовыйПериодНачало КАК БазовыйПериодНачало,
				   |	ВТДвижения.БазовыйПериодКонец КАК БазовыйПериодКонец,
				   |	ВТДвижения.Активность КАК Активность,
				   |	ВТДвижения.Сторно КАК Сторно,
				   |	ВТДвижения.ФизическоеЛицо КАК ФизическоеЛицо,
				   |	ВТДвижения.Подразделение КАК Подразделение,
				   |	ВТДвижения.Должность КАК Должность,
				   |	ВТДвижения.Результат КАК Результат,
				   |	ВТДвижения.Размер КАК Размер,
				   |	ВТПриоритетыВидовРасчета.Приоритет КАК Приоритет
				   |ПОМЕСТИТЬ ВТНаборЗаписей
				   |ИЗ
				   |	ВТДвижения КАК ВТДвижения
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритетыВидовРасчета КАК ВТПриоритетыВидовРасчета
				   |		ПО ВТДвижения.ВидРасчета = ВТПриоритетыВидовРасчета.ВидРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки,
				   |	ВТНаборЗаписей.ВидРасчета КАК ВидРасчета,
				   |	ВТНаборЗаписей.ВидРасчета.СпособРасчета КАК СпособРасчета,
				   |	ВТНаборЗаписей.Приоритет КАК Приоритет
				   |ИЗ
				   |	ВТНаборЗаписей КАК ВТНаборЗаписей
				   |
				   |УПОРЯДОЧИТЬ ПО
				   |	Приоритет,
				   |	НомерСтроки
				   |ИТОГИ ПО
				   |	Приоритет
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТДвижения
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТВидыРасчетаДокумента
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТВедущиеВидыРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТПриоритетыВидовРасчета";
	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПриоритет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Внешний цикл по категориям
	Пока ВыборкаПриоритет.Следующий() Цикл   
		
		//Запрос для получения базы и данных графика для записей с текущей категорией 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТЗаписиДляРасчета
		|ИЗ
		|	ВТНаборЗаписей КАК ВТНаборЗаписей
		|ГДЕ
		|	ВТНаборЗаписей.Приоритет = &Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.ОтработаноДнейБаза
		|ПОМЕСТИТЬ ВТБазаВОсновныхНачислениях
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
		|			&ИЗмерения_ДополнительныеНачисления,
		|			&Измерения_ОсновныеНачисления,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК ДополнительныеНачисленияБазаОсновныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеНачисленияБазаДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисленияБазаДополнительныеНачисления.РезультатБаза
		|ПОМЕСТИТЬ ВТБазаВДополнительныхНачислениях
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления.БазаДополнительныеНачисления(
		|			&ИЗмерения_ДополнительныеНачисления,
		|			&ИЗмерения_ДополнительныеНачисления,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК ДополнительныеНачисленияБазаДополнительныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗаписиДляРасчета.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.РезультатБаза, 0) + ЕСТЬNULL(ВТБазаВДополнительныхНачислениях.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза
		|ИЗ
		|	ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВОсновныхНачислениях КАК ВТБазаВОсновныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВОсновныхНачислениях.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВДополнительныхНачислениях КАК ВТБазаВДополнительныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВДополнительныхНачислениях.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВОсновныхНачислениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВДополнительныхНачислениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТЗаписиДляРасчета";
		Измерения_ДополнительныеНачисления = Новый Массив;
		Измерения_ДополнительныеНачисления.Добавить("ФизическоеЛицо");
		Измерения_ДополнительныеНачисления.Добавить("Подразделение");
		Измерения_ДополнительныеНачисления.Добавить("Должность");

		Измерения_ОсновныеНачисления = Новый Массив;
		Измерения_ОсновныеНачисления.Добавить("ФизическоеЛицо");
		Измерения_ОсновныеНачисления.Добавить("Подразделение");
		Измерения_ОсновныеНачисления.Добавить("Должность");

		Запрос.УстановитьПараметр("Измерения_ДополнительныеНачисления", Измерения_ДополнительныеНачисления);
		Запрос.УстановитьПараметр("Измерения_ОсновныеНачисления", Измерения_ОсновныеНачисления);
		Запрос.УстановитьПараметр("Приоритет", ВыборкаПриоритет.Приоритет);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);

		РезультатЗапроса = Запрос.Выполнить();
		
		//Выборка с данными для записей текущей категории
		Выборка_ДанныеГрафика_База = РезультатЗапроса.Выбрать();
		
		
		//Вложенный цикл по записям текущей категории
		ВыборкаДетальныеЗаписи = ВыборкаПриоритет.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Запись = НаборЗаписей.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);

			Выборка_ДанныеГрафика_База.НайтиСледующий(ВыборкаДетальныеЗаписи.НомерСтроки, "НомерСтроки");

			РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись);

		КонецЦикла;

		НаборЗаписей.Записать( , , Ложь, Ложь);

	КонецЦикла;

	НаборЗаписей.Записать( , , Ложь, Истина);

КонецПроцедуры

Процедура ПереРассчитатьРесурсы_Удержания(НаборЗаписей) Экспорт

	//НаборЗаписей = Движения.Удержания;
	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	
    //Получаем упорядоченные по категориям данные текущего набора записей, записанные в БД
	Менеджер = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст = "ВЫБРАТЬ
				   |	Перерасчет.ВидРасчета,
				   |	Перерасчет.Работник
				   |ПОМЕСТИТЬ ВТПерерасчет
				   |ИЗ
				   |	РегистрРасчета.Удержания.Перерасчет КАК Перерасчет
				   |ГДЕ
				   |	Перерасчет.ОбъектПерерасчета = &Регистратор
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	Удержания.НомерСтроки КАК НомерСтроки,
				   |	Удержания.ВидРасчета КАК ВидРасчета,
				   |	Удержания.ПериодРегистрации КАК ПериодРегистрации,
				   |	Удержания.Регистратор КАК Регистратор,
				   |	Удержания.БазовыйПериодНачало КАК БазовыйПериодНачало,
				   |	Удержания.БазовыйПериодКонец КАК БазовыйПериодКонец,
				   |	Удержания.Активность КАК Активность,
				   |	Удержания.Сторно КАК Сторно,
				   |	Удержания.ФизическоеЛицо КАК ФизическоеЛицо,
				   |	Удержания.Подразделение КАК Подразделение,
				   |	Удержания.Должность КАК Должность,
				   |	Удержания.Результат КАК Результат,
				   |	Удержания.Размер КАК Размер
				   |ПОМЕСТИТЬ ВТДвижения
				   |ИЗ
				   |	РегистрРасчета.Удержания КАК Удержания
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПерерасчет КАК ВТПерерасчет
				   |		ПО Удержания.ВидРасчета = ВТПерерасчет.ВидРасчета
				   |			И Удержания.ФизическоеЛицо = ВТПерерасчет.Работник
				   |ГДЕ
				   |	Удержания.Регистратор = &Регистратор
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	ВТДвижения.ВидРасчета КАК ВидРасчета
				   |ПОМЕСТИТЬ ВТВидыРасчетаДокумента
				   |ИЗ
				   |	ВТДвижения КАК ВТДвижения
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТВидыРасчетаДокумента.ВидРасчета КАК ВидРасчета,
				   |	УдержанияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета
				   |ПОМЕСТИТЬ ВТВедущиеВидыРасчета
				   |ИЗ
				   |	ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
				   |		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания.ВедущиеВидыРасчета КАК УдержанияВедущиеВидыРасчета
				   |		ПО ВТВидыРасчетаДокумента.ВидРасчета = УдержанияВедущиеВидыРасчета.Ссылка
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТВедущиеВидыРасчета.ВидРасчета КАК ВидРасчета,
				   |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТВидыРасчетаДокумента.ВидРасчета) КАК Приоритет
				   |ПОМЕСТИТЬ ВТПриоритетыВидовРасчета
				   |ИЗ
				   |	ВТВедущиеВидыРасчета КАК ВТВедущиеВидыРасчета
				   |		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
				   |		ПО ВТВедущиеВидыРасчета.ВедущийВидРасчета = ВТВидыРасчетаДокумента.ВидРасчета
				   |
				   |СГРУППИРОВАТЬ ПО
				   |	ВТВедущиеВидыРасчета.ВидРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТДвижения.НомерСтроки КАК НомерСтроки,
				   |	ВТДвижения.ВидРасчета КАК ВидРасчета,
				   |	ВТДвижения.ПериодРегистрации КАК ПериодРегистрации,
				   |	ВТДвижения.Регистратор КАК Регистратор,
				   |	ВТДвижения.БазовыйПериодНачало КАК БазовыйПериодНачало,
				   |	ВТДвижения.БазовыйПериодКонец КАК БазовыйПериодКонец,
				   |	ВТДвижения.Активность КАК Активность,
				   |	ВТДвижения.Сторно КАК Сторно,
				   |	ВТДвижения.ФизическоеЛицо КАК ФизическоеЛицо,
				   |	ВТДвижения.Подразделение КАК Подразделение,
				   |	ВТДвижения.Должность КАК Должность,
				   |	ВТДвижения.Результат КАК Результат,
				   |	ВТДвижения.Размер КАК Размер,
				   |	ВТПриоритетыВидовРасчета.Приоритет КАК Приоритет
				   |ПОМЕСТИТЬ ВТНаборЗаписей
				   |ИЗ
				   |	ВТДвижения КАК ВТДвижения
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритетыВидовРасчета КАК ВТПриоритетыВидовРасчета
				   |		ПО ВТДвижения.ВидРасчета = ВТПриоритетыВидовРасчета.ВидРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки,
				   |	ВТНаборЗаписей.ВидРасчета КАК ВидРасчета,
				   |	ВТНаборЗаписей.ВидРасчета.СпособРасчета КАК СпособРасчета,
				   |	ВТНаборЗаписей.Приоритет КАК Приоритет
				   |ИЗ
				   |	ВТНаборЗаписей КАК ВТНаборЗаписей
				   |
				   |УПОРЯДОЧИТЬ ПО
				   |	Приоритет,
				   |	НомерСтроки
				   |ИТОГИ ПО
				   |	Приоритет
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТДвижения
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТВидыРасчетаДокумента
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТВедущиеВидыРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТПриоритетыВидовРасчета";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПриоритет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Внешний цикл по категориям
	Пока ВыборкаПриоритет.Следующий() Цикл
		
		//Запрос для получения базы и данных графика для записей с текущей категорией 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТНаборЗаписей.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТЗаписиДляРасчета
		|ИЗ
		|	ВТНаборЗаписей КАК ВТНаборЗаписей
		|ГДЕ
		|	ВТНаборЗаписей.Приоритет = &Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УдержанияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	УдержанияБазаОсновныеНачисления.РезультатБаза КАК РезультатБаза,
		|	УдержанияБазаОсновныеНачисления.ОтработаноДнейБаза КАК ОтработаноДнейБаза
		|ПОМЕСТИТЬ ВТБазаВОсновныхНачислениях
		|ИЗ
		|	РегистрРасчета.Удержания.БазаОсновныеНачисления(
		|			&ИЗмерения_Удержания,
		|			&Измерения_ОсновныеНачисления,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК УдержанияБазаОсновныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УдержанияБазаДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	УдержанияБазаДополнительныеНачисления.РезультатБаза КАК РезультатБаза
		|ПОМЕСТИТЬ ВТБазаВДополнительныхНачислениях
		|ИЗ
		|	РегистрРасчета.Удержания.БазаДополнительныеНачисления(
		|			&ИЗмерения_Удержания,
		|			&ИЗмерения_ДополнительныеНачисления,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|				И НомерСтроки В
		|					(ВЫБРАТЬ
		|						ВТЗаписиДляРасчета.НомерСтроки
		|					ИЗ
		|						ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК УдержанияБазаДополнительныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗаписиДляРасчета.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.РезультатБаза, 0) + ЕСТЬNULL(ВТБазаВДополнительныхНачислениях.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза
		|ИЗ
		|	ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВОсновныхНачислениях КАК ВТБазаВОсновныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВОсновныхНачислениях.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВДополнительныхНачислениях КАК ВТБазаВДополнительныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВДополнительныхНачислениях.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВОсновныхНачислениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВДополнительныхНачислениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТЗаписиДляРасчета";

		Измерения_ДополнительныеНачисления = Новый Массив;
		Измерения_ДополнительныеНачисления.Добавить("ФизическоеЛицо");
		Измерения_ДополнительныеНачисления.Добавить("Подразделение");
		Измерения_ДополнительныеНачисления.Добавить("Должность");

		Измерения_ОсновныеНачисления = Новый Массив;
		Измерения_ОсновныеНачисления.Добавить("ФизическоеЛицо");
		Измерения_ОсновныеНачисления.Добавить("Подразделение");
		Измерения_ОсновныеНачисления.Добавить("Должность");

		Измерения_Удержания = Новый Массив;
		Измерения_Удержания.Добавить("ФизическоеЛицо");
		Измерения_Удержания.Добавить("Подразделение");
		Измерения_Удержания.Добавить("Должность");

		Запрос.УстановитьПараметр("Измерения_ДополнительныеНачисления", Измерения_ДополнительныеНачисления);
		Запрос.УстановитьПараметр("Измерения_ОсновныеНачисления", Измерения_ОсновныеНачисления);
		Запрос.УстановитьПараметр("Измерения_Удержания", Измерения_Удержания);
		Запрос.УстановитьПараметр("Приоритет", ВыборкаПриоритет.Приоритет);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);

		РезультатЗапроса = Запрос.Выполнить();
		
		//Выборка с данными для записей текущей категории
		Выборка_ДанныеГрафика_База = РезультатЗапроса.Выбрать();
		
		
		//Вложенный цикл по записям текущей категории
		ВыборкаДетальныеЗаписи = ВыборкаПриоритет.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Запись = НаборЗаписей.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);

			Выборка_ДанныеГрафика_База.НайтиСледующий(ВыборкаДетальныеЗаписи.НомерСтроки, "НомерСтроки");

			РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись);

		КонецЦикла;

		НаборЗаписей.Записать( , , Ложь, Ложь);

	КонецЦикла;

	НаборЗаписей.Записать( , , Ложь, Истина);

КонецПроцедуры

#КонецОбласти